plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven'
    id 'jacoco'
    id 'maven-publish'

    id 'org.ajoberstar.grgit' version '4.0.1'
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'com.github.johnrengelman.shadow'

group 'com.webank.wecross'
version '1.0.0-rc3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

googleJavaFormat {
    options style: 'AOSP'
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}

verifyGoogleJavaFormat {
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}

configurations {
    all*.exclude group: 'org.java-websocket', module: 'Java-WebSocket'
    all*.exclude group: 'org.antlr', module: '*'
    all*.exclude group: 'de.vandermeer', module: '*'
    all*.exclude group: 'net.bytebuddy', module: 'byte-buddy'
    all*.exclude group: 'com.alibaba', module: 'druid'
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    all*.exclude group: 'org.mockito', module: 'mockito-core'
    all*.exclude group: 'io.reactivex', module: 'rxjava'
    all*.exclude group: 'org.ethereum', module: 'solcJ-all'
    all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'

    integTestImplementation.extendsFrom implementation
    integTestTestImplementation.extendsFrom testImplementation

    all*.resolutionStrategy {
        force 'com.alibaba:fastjson:1.2.67'
    }
}

dependencies {
    // implementation 'com.webank:wecross-java-stub:1.0.0-rc2'
    implementation 'com.webank:wecross-java-stub:1.0.0-rc3-0615-SNAPSHOT'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'commons-codec:commons-codec:1.14'
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'org.fisco-bcos:web3sdk:2.4.0'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.25'
    // testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.4.RELEASE'
}

sourceSets {

    main {
        resources {
            exclude '/*'
        }
    }

    // Add the integration test directory
    integTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }

//        resources {
//            srcDir file('src/integTest/resources')
//        }
    }
}

shadowJar {
    mergeServiceFiles()
    minimize()
}


task makeJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar, dependsOn: shadowJar) {

    destinationDir file('dist/apps')
    archiveName project.name + "-" + project.version + '.jar'
    exclude '**/*.xml'
    exclude '**/*.toml'
    exclude '**/*.properties'
    exclude '**/*.yml'
    exclude '**/BCOSGMStubFactory.*'
    exclude '**/performance/guomi/*'

    manifest {
        attributes(
                'Main-Class': 'com.webank.wecross.stub.bcos.normal.BCOSStubFactory',
                'version': project.version,
        )
    }

    relocate 'org.fisco.bcos', 'org.fisco.bcos.wecross.normal'
    relocate 'com.webank.wecross.stub.bcos', 'com.webank.wecross.stub.bcos.normal'

    from zipTree(shadowJar.archivePath)
}

task makeGMJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar, dependsOn: shadowJar) {

    destinationDir file('dist/apps')
    archiveName project.name + "-gm-" + project.version + '.jar'
    exclude '**/*.xml'
    exclude '**/*.toml'
    exclude '**/*.properties'
    exclude '**/*.yml'
    exclude '**/BCOSStubFactory.*'
    exclude '**/performance/normal/*'

    manifest {
        attributes(
                'Main-Class': 'com.webank.wecross.stub.bcos.guomi.BCOSGMStubFactory',
                'version': project.version,
        )
    }

    relocate 'org.fisco.bcos', 'org.fisco.bcos.wecross.guomi'
    relocate 'com.webank.wecross.stub.bcos', 'com.webank.wecross.stub.bcos.guomi'

    from zipTree(shadowJar.archivePath)
}

jar.enabled = false
project.tasks.assemble.dependsOn(makeJar)
project.tasks.assemble.dependsOn(makeGMJar)

task integTest(type: Test) {
    description = 'Runs integration tests.'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integTest
check.dependsOn.remove(integTest)

// clean dist and log dir
task cleanDistAndLogDir(type: Delete) {
    delete 'dist'
    delete 'log'
    delete 'out'
}

tasks.clean.dependsOn(tasks.cleanDistAndLogDir)

test {
    testLogging {
        showStandardStreams = true
        events 'passed', 'skipped', 'failed'
    }

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        //html.destination file("${buildDir}/jacocoHtml")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.webank'
            artifactId = 'wecross-bcos-stub'
            from components.java
            artifacts = [shadowJar]

            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = "WeCross-BCOS-Stub"
                description = "Stub interface for WeCross"
                url = "https://github.com/WeBankFinTech/WeCross"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "Shareong"
                        name = "Shareong"
                        email = "740310627@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/WeBankFinTech/WeCross.git"
                    developerConnection = "scm:git:ssh://github.com/WeBankFinTech/WeCross.git"
                    url = "https://github.com/WeBankFinTech/WeCross"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            //*
            def NEXUS_USERNAME = ''
            def NEXUS_PASSWORD = ''
            //*/
            credentials {
                username = NEXUS_USERNAME
                password = NEXUS_PASSWORD
            }

        }
    }
}
